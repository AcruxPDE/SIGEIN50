-- =============================================
-- Proyecto: Sigein 5.0
-- Copyright (c) - Acrux - 2015
-- Author: Margarita Salcedo
-- CRETAE date: 14/09/2015
-- Description: Inserta un nuevo C_CAMPO_ADICIONAL
-- =============================================
-- 20/11/2015 MS - Se cambia el valor de retorno 
-- =============================================

CREATE PROCEDURE [ADM].[SPE_INSERTA_ACTUALIZA_C_CAMPO_ADICIONAL] 
		  @XML_RESULTADO XML = '' OUT      --APLICA PARA REGRESAR UN NÚMERO 0 PARA ERROR Y 1 PARA CORRECTO
    	, @PIN_ID_CAMPO AS int
		, @PIN_CL_CAMPO AS nvarchar(32)
		, @PIN_NB_CAMPO AS nvarchar(100)
		, @PIN_DS_CAMPO AS nvarchar(200)
		, @PIN_FG_REQUERIDO AS bit
		, @PIN_NO_VALOR_DEFECTO AS nvarchar(200)
		, @PIN_CL_TIPO_DATO AS nvarchar(20)
		, @PIN_CL_DIMENSION AS nvarchar(20)
		, @PIN_CL_TABLA_REFERENCIA AS nvarchar(50)
		, @PIN_CL_ESQUEMA_REFERENCIA AS nvarchar(10)
		, @PIN_CL_USUARIO_APP_CREA AS nvarchar(50)
		, @PIN_CL_USUARIO_APP_MODIFICA AS nvarchar(50)
		, @PIN_NB_PROGRAMA_CREA AS nvarchar(50)
		, @PIN_NB_PROGRAMA_MODIFICA AS nvarchar(50)
		, @PIN_FG_MOSTRARAS AS bit 
		, @PIN_ID_CATALOGO_LIST AS int
		, @PIN_FG_ADICIONAL AS bit
		, @PIN_TIPO_TRANSACCION CHAR(1)             --I=INSERCIÓN   A=ACTUALIZACIÓN

AS 
BEGIN  
	--SE DECLARA E INICIALIZA LA VARIABLE QUE NOS INDICARA SI GENERAMOS LA TRANSACCION EN ESTE SP
	DECLARE @V_EXIST_TRAN BIT = 0
	,@CFE_SISTEMA DATETIME = dbo.F_GETDATE()

    	BEGIN TRY
		--SE VERIFICA SI EXISTE UNA TRANSACCION EN EJECUCION
		IF (@@TRANCOUNT = 0) 
		BEGIN
			--EN CASO DE QUE NO SE INICIALIZA LA TRANSACCION
			BEGIN TRANSACTION
			--SE EDITA LA VARIABLE QUE INDICA QUE SE INICIO LA TRANSACCION EN ESTE BLOQUE PARA CANCELARLA SI ES NECESARIO
			SET @V_EXIST_TRAN = 1
		END	
		--SE VERIFICA SI SE INSERTA EL REGISTRO O SE ACTUALIZARA SEGUN LA VARIABLE DE TIPO DE TRANSACCION  QUE RECIBE EL SP
		IF @PIN_TIPO_TRANSACCION='I'
	    	BEGIN
			--SE INSERTA EL REGISTRO EN LA TABLA  ADM.C_CAMPO_ADICIONAL
			INSERT INTO ADM.C_CAMPO_ADICIONAL
					   ([CL_CAMPO]
						, [NB_CAMPO]
						, [DS_CAMPO]
						, [FG_REQUERIDO]
						, [NO_VALOR_DEFECTO]
						, [CL_TIPO_DATO]
						, [CL_DIMENSION]
						, [CL_TABLA_REFERENCIA]
						, [CL_ESQUEMA_REFERENCIA]
						, [FE_CREACION]
						, [CL_USUARIO_APP_CREA]
						, [NB_PROGRAMA_CREA]
					)
			VALUES
					   (@PIN_CL_CAMPO
						, @PIN_NB_CAMPO
						, @PIN_DS_CAMPO
						, @PIN_FG_REQUERIDO
						, @PIN_NO_VALOR_DEFECTO
						, @PIN_CL_TIPO_DATO
						, @PIN_CL_DIMENSION
						, @PIN_CL_TABLA_REFERENCIA
						, @PIN_CL_ESQUEMA_REFERENCIA
						,  @CFE_SISTEMA
						, @PIN_CL_USUARIO_APP_CREA
						, @PIN_NB_PROGRAMA_CREA
					)			
		END ELSE BEGIN
			--SE ACTUALIZA EL REGISTRO EN LA TABLA  ADM.C_CAMPO_ADICIONAL
			UPDATE ADM.C_CAMPO_ADICIONAL SET
				[CL_CAMPO] = @PIN_CL_CAMPO
				, [NB_CAMPO] = @PIN_NB_CAMPO
				, [DS_CAMPO] = @PIN_DS_CAMPO
				, [FG_REQUERIDO] = @PIN_FG_REQUERIDO
				, [NO_VALOR_DEFECTO] = @PIN_NO_VALOR_DEFECTO
				, [CL_TIPO_DATO] = @PIN_CL_TIPO_DATO
				, [CL_DIMENSION] = @PIN_CL_DIMENSION
				, [CL_TABLA_REFERENCIA] = @PIN_CL_TABLA_REFERENCIA
				, [CL_ESQUEMA_REFERENCIA] = @PIN_CL_ESQUEMA_REFERENCIA
				, [FE_MODIFICACION] = @CFE_SISTEMA
				, [CL_USUARIO_APP_MODIFICA] = @PIN_CL_USUARIO_APP_MODIFICA
				, [NB_PROGRAMA_MODIFICA] = @PIN_NB_PROGRAMA_MODIFICA
			       
			WHERE [ID_CAMPO] = @PIN_ID_CAMPO
									
		END
		--SE DEVUELVE LA VARIABLE DE RETORNO INDICANDO QUE TODO SE REALIZO CORRECTAMENTE
		SET @XML_RESULTADO = DBO.F_ERROR_CREAR_ENCABEZADO( @@ROWCOUNT, ERROR_NUMBER(), 'SUCCESSFUL')
		SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'Proceso exitoso', 'ES')
		SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'Successful Process', 'EN')
		--SI SE GENERO UNA TRANSACCION EN ESTE BLOQUE LA TERMINARA
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT	
	END TRY
	BEGIN CATCH		
		--SI OCURRIO UN ERROR Y SE INICIO UNA TRANSACCION ENE ESTE BLOQUE SE CANCELARA LA TRANSACCION
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
		--SE INDICA EN LA VARIABLE DE RETORNO QUE OCURRIO UN ERROR
		
		--SE INSERTA EL ERROR EN LA TABLA		
		DECLARE @ERROR_CLAVE INT  = 	ERROR_NUMBER()
		DECLARE @ERROR_MENSAJE NVARCHAR(250)  = 	 ERROR_MESSAGE()

		SET @XML_RESULTADO = DBO.F_ERROR_CREAR_ENCABEZADO( @@ROWCOUNT, @ERROR_CLAVE, 'ERROR')
		SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'Ocurrió un error al procesar el registro', 'ES')
		SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'Ocurrió un error al procesar el registro', 'EN')
			
	END CATCH
END
