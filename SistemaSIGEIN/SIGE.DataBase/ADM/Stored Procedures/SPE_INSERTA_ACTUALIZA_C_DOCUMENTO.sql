-- =============================================
-- Proyecto: Sistema Nomina
-- Copyright (c) - Acrux - 2014
-- Author: Jesús Vázquez Pereyra
-- CRETAE date: 05/11/2015
-- Description: Inserta un nuevo registro en la tabla C_DOCUMENTO
-- =============================================
CREATE PROCEDURE [ADM].[SPE_INSERTA_ACTUALIZA_C_DOCUMENTO] 
	       @XML_RESULTADO XML = '' OUT      --APLICA PARA REGRESAR UN NÚMERO 0 PARA ERROR Y 1 PARA CORRECTO
    	, @PIN_ID_DOCUMENTO AS int
		, @PIN_NB_DOCUMENTO AS nvarchar(50)
		, @PIN_CL_DOCUMENTO AS nvarchar(20)
		, @PIN_ID_CANDIDATO AS int
		, @PIN_ID_EMPLEADO AS int
		, @PIN_CL_RUTA AS nvarchar(512)
		, @PIN_CL_TIPO_RUTA AS nvarchar(10)
		, @PIN_FE_RECEPCION AS datetime
		, @PIN_ID_INSTITUCION AS int
		, @PIN_ID_BITACORA AS int
		, @PIN_FG_ACTIVO AS bit
		, @PIN_CL_USUARIO_APP_CREA AS nvarchar(50)
		, @PIN_CL_USUARIO_APP_MODIFICA AS nvarchar(50)
		, @PIN_NB_PROGRAMA_CREA AS nvarchar(50)
		, @PIN_NB_PROGRAMA_MODIFICA AS nvarchar(50)
		, @PIN_TIPO_TRANSACCION CHAR(1)             --I=INSERCIÓN   A=ACTUALIZACIÓN
		, @P_ARCHIVO VARBINARY(MAX)
		, @P_RUTA NVARCHAR(20)
AS 
BEGIN  
	--SE DECLARA E INICIALIZA LA VARIABLE QUE NOS INDICARA SI GENERAMOS LA TRANSACCION EN ESTE SP
	DECLARE @V_EXIST_TRAN BIT = 0,
			@V_RUTA VARCHAR(100),
			@R_ID_FILESTREAM UNIQUEIDENTIFIER,
			@V_ID UNIQUEIDENTIFIER
	,@CFE_SISTEMA DATETIME = dbo.F_GETDATE()

    	BEGIN TRY
		--SE VERIFICA SI EXISTE UNA TRANSACCION EN EJECUCION
		IF (@@TRANCOUNT = 0) 
		BEGIN
			--EN CASO DE QUE NO SE INICIALIZA LA TRANSACCION
			BEGIN TRANSACTION
			--SE EDITA LA VARIABLE QUE INDICA QUE SE INICIO LA TRANSACCION EN ESTE BLOQUE PARA CANCELARLA SI ES NECESARIO
			SET @V_EXIST_TRAN = 1
		END	
		--SE VERIFICA SI SE INSERTA EL REGISTRO O SE ACTUALIZARA SEGUN LA VARIABLE DE TIPO DE TRANSACCION  QUE RECIBE EL SP
		IF @PIN_TIPO_TRANSACCION='I'
	    	BEGIN
			--SE INSERTA EL REGISTRO EN LA TABLA  ADM.C_DOCUMENTO

			SET @V_RUTA = '\Home\' + @P_RUTA
			EXECUTE ADM.SP_CREA_RUTA @P_FS_CARPETA_MULTIMEDIA = @V_RUTA
										,@R_ID_FILESTREAM = @R_ID_FILESTREAM OUTPUT

			SET @V_ID = NEWID()

			SET @V_RUTA = FileTableRootPath() + @V_RUTA

			INSERT INTO FS_SISTEMA_DE_ARCHIVOS (stream_id, file_stream, name, path_locator, is_archive)
			SELECT @V_ID, @P_ARCHIVO, @PIN_NB_DOCUMENTO, dbo.GetNewPathLocator(GetPathLocator(@V_RUTA)), 1

			SET @PIN_CL_TIPO_RUTA = 'FILETABLE'

			INSERT INTO ADM.C_DOCUMENTO
					   ([NB_DOCUMENTO]
						, [CL_DOCUMENTO]
						, [ID_CANDIDATO]
						, [ID_EMPLEADO]
						, [CL_RUTA]
						, [CL_TIPO_RUTA]
						, [FE_RECEPCION]
						, [ID_INSTITUCION]
						, [ID_BITACORA]
						, [FG_ACTIVO]
						, [FE_CREACION]
						, [CL_USUARIO_APP_CREA]
						, [NB_PROGRAMA_CREA]
					)
			VALUES
					   (@PIN_NB_DOCUMENTO
						, @PIN_CL_DOCUMENTO
						, @PIN_ID_CANDIDATO
						, @PIN_ID_EMPLEADO
						, @V_ID
						, @PIN_CL_TIPO_RUTA
						, @CFE_SISTEMA
						, @PIN_ID_INSTITUCION
						, @PIN_ID_BITACORA
						, @PIN_FG_ACTIVO
						,  @CFE_SISTEMA
						, @PIN_CL_USUARIO_APP_CREA
						, @PIN_NB_PROGRAMA_CREA
					)			
		END ELSE BEGIN
			--SE ACTUALIZA EL REGISTRO EN LA TABLA  ADM.C_DOCUMENTO
			UPDATE ADM.C_DOCUMENTO SET
				[NB_DOCUMENTO] = @PIN_NB_DOCUMENTO
				, [CL_DOCUMENTO] = @PIN_CL_DOCUMENTO
				, [ID_CANDIDATO] = @PIN_ID_CANDIDATO
				, [ID_EMPLEADO] = @PIN_ID_EMPLEADO
				, [CL_RUTA] = @PIN_CL_RUTA
				, [CL_TIPO_RUTA] = @PIN_CL_TIPO_RUTA
				--, [FE_RECEPCION] = @PIN_FE_RECEPCION  -- NO SE DEBE MODIFICAR LA FECHA EN QUE SE RECIBE EL DOCUMENTO 
				, [ID_INSTITUCION] = @PIN_ID_INSTITUCION
				, [ID_BITACORA] = @PIN_ID_BITACORA
				, [FG_ACTIVO] = @PIN_FG_ACTIVO
				, [FE_MODIFICACION] = @CFE_SISTEMA
				, [CL_USUARIO_APP_MODIFICA] = @PIN_CL_USUARIO_APP_MODIFICA
				, [NB_PROGRAMA_MODIFICA] = @PIN_NB_PROGRAMA_MODIFICA
			       
			WHERE [ID_DOCUMENTO] = @PIN_ID_DOCUMENTO
									
		END
		--SE DEVUELVE LA VARIABLE DE RETORNO INDICANDO QUE TODO SE REALIZO CORRECTAMENTE
		SET @XML_RESULTADO = DBO.F_ERROR_CREAR_ENCABEZADO( @@ROWCOUNT, ERROR_NUMBER(), 'SUCCESSFUL')
		SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'Proceso exitoso', 'ES')
		SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'Successful Process', 'EN')
		--SI SE GENERO UNA TRANSACCION EN ESTE BLOQUE LA TERMINARA
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT	
	END TRY
	BEGIN CATCH		
		--SI OCURRIO UN ERROR Y SE INICIO UNA TRANSACCION ENE ESTE BLOQUE SE CANCELARA LA TRANSACCION
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
		--SE INDICA EN LA VARIABLE DE RETORNO QUE OCURRIO UN ERROR
		
		--SE INSERTA EL ERROR EN LA TABLA		
		DECLARE @ERROR_CLAVE INT  = 	ERROR_NUMBER()
		DECLARE @ERROR_MENSAJE NVARCHAR(250)  = 	 ERROR_MESSAGE()
		
		SET @XML_RESULTADO = DBO.F_ERROR_CREAR_ENCABEZADO( @@ROWCOUNT, @ERROR_CLAVE, 'ERROR')
		SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'Ocurrió un error al procesar el registro', 'ES')
		SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'Ocurrió un error al procesar el registro', 'EN')
			
	END CATCH
END
