-- =============================================
-- Proyecto: Sistema SIGEIN 5.0
-- Copyright (c) - Acrux - 2015
-- Author: Julio Díaz
-- CRETAE date: 1/12/2015
-- Description: Ejecuta la operación para inserción o actualización del usuario
-- =============================================
CREATE PROCEDURE [ADM].[SPE_INSERTA_ACTUALIZA_USUARIO] 
		  @XML_RESULTADO XML OUT     --APLICA PARA REGRESAR UN NÚMERO 0 PARA ERROR Y 1 PARA CORRECTO
    	, @PIN_CL_USER AS NVARCHAR(50) = NULL
		, @PIN_NB_USUARIO AS NVARCHAR(300)
		, @PIN_NB_PASSWORD AS NVARCHAR(100)
		, @PIN_NB_CORREO_ELECTRONICO AS NVARCHAR(500)
		, @PIN_FG_ACTIVO AS BIT
		, @PIN_FG_CAMBIAR_PASSWORD AS BIT
		, @PIN_ID_ROL AS INT
		, @PIN_ID_EMPLEADO AS INT = NULL
		, @PIN_CL_USUARIO AS NVARCHAR(50)
		, @PIN_NB_PROGRAMA AS NVARCHAR(50)
		, @PIN_TIPO_TRANSACCION CHAR(1)             --I=INSERCIÓN   A=ACTUALIZACIÓN
AS 
BEGIN  
	--SE DECLARA E INICIALIZA LA VARIABLE QUE NOS INDICARA SI GENERAMOS LA TRANSACCION EN ESTE SP
	DECLARE @V_EXIST_TRAN BIT = 0
		,@CL_USUARIO NVARCHAR(50)
		,@FE_SISTEMA DATETIME = GETDATE()

    BEGIN TRY
		--SE VERIFICA SI EXISTE UNA TRANSACCION EN EJECUCION
		IF (@@TRANCOUNT = 0) BEGIN
			--EN CASO DE QUE NO SE INICIALIZA LA TRANSACCION
			BEGIN TRANSACTION
			--SE EDITA LA VARIABLE QUE INDICA QUE SE INICIO LA TRANSACCION EN ESTE BLOQUE PARA CANCELARLA SI ES NECESARIO
			SET @V_EXIST_TRAN = 1
		END

		IF @PIN_TIPO_TRANSACCION = 'I' BEGIN
			INSERT INTO ADM.C_USUARIO (
				CL_USUARIO
				, NB_USUARIO
				, NB_CORREO_ELECTRONICO
				, NB_PASSWORD
				, ID_ROL
				, ID_EMPLEADO
				, FE_INACTIVO
				, FG_ACTIVO
				, FG_CAMBIAR_PASSWORD
				, FE_CREACION
				, CL_USUARIO_APP_CREA
				, NB_PROGRAMA_CREA
			) VALUES (
				@PIN_CL_USER
				, @PIN_NB_USUARIO
				, @PIN_NB_CORREO_ELECTRONICO
				, @PIN_NB_PASSWORD
				, @PIN_ID_ROL
				, @PIN_ID_EMPLEADO
				, CASE WHEN @PIN_FG_ACTIVO = 0 THEN @FE_SISTEMA ELSE NULL END
				, @PIN_FG_ACTIVO
				, 0
				, @FE_SISTEMA
				, @PIN_CL_USUARIO
				, @PIN_NB_PROGRAMA
			)

			SET @XML_RESULTADO = DBO.F_ERROR_CREAR_ENCABEZADO(@@ROWCOUNT, 1, 'SUCCESSFUL')
			SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'El usuario se agregó satisfactoriamente', 'ES')
			SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'El usuario se agregó satisfactoriamente', 'EN')
		END

		IF @PIN_TIPO_TRANSACCION = 'A' AND @PIN_ID_ROL IS NOT NULL BEGIN

			SET @CL_USUARIO = @PIN_CL_USER

			UPDATE ADM.C_USUARIO
			SET NB_USUARIO = @PIN_NB_USUARIO
				, NB_CORREO_ELECTRONICO = @PIN_NB_CORREO_ELECTRONICO
				, NB_PASSWORD = CASE WHEN @PIN_FG_CAMBIAR_PASSWORD = 1 THEN @PIN_NB_PASSWORD ELSE NB_PASSWORD END
				, ID_ROL = @PIN_ID_ROL
				, ID_EMPLEADO = @PIN_ID_EMPLEADO
				, FE_INACTIVO = CASE WHEN FG_ACTIVO = 1 AND @PIN_FG_ACTIVO = 0 THEN @FE_SISTEMA ELSE FE_INACTIVO END
				, FG_ACTIVO = @PIN_FG_ACTIVO
				, FE_MODIFICACION = @FE_SISTEMA
				, CL_USUARIO_APP_MODIFICA = @PIN_CL_USUARIO
				, NB_PROGRAMA_MODIFICA = @PIN_NB_PROGRAMA
			WHERE CL_USUARIO = @CL_USUARIO

			SET @XML_RESULTADO = DBO.F_ERROR_CREAR_ENCABEZADO(@@ROWCOUNT, 1, 'SUCCESSFUL')
			SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'El usuario se editó satisfactoriamente', 'ES')
			SET @XML_RESULTADO = DBO.F_ERROR_INSERTAR_MENSAJES(@XML_RESULTADO, 'El usuario se editó satisfactoriamente', 'EN')
		END

		
		-- EL XML DEVUELVE EL ERROR INDICADO POR SQL Y UN MSJ DE ERROR GENÉRICO

		--SI SE GENERO UNA TRANSACCION EN ESTE BLOQUE LA TERMINARA
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			COMMIT	
	END TRY
	BEGIN CATCH
		--SI OCURRIO UN ERROR Y SE INICIO UNA TRANSACCION ENE ESTE BLOQUE SE CANCELARA LA TRANSACCION
		IF (@@TRANCOUNT > 0 AND @V_EXIST_TRAN = 1)
			ROLLBACK
		
		-- EL XML DEVUELVE EL ERROR INDICADO POR SQL Y UN MSJ DE ERROR GENÉRICO
		SET @XML_RESULTADO = DBO.F_ERROR_MENSAJES(ERROR_NUMBER(), ERROR_MESSAGE())
			
	END CATCH
END
