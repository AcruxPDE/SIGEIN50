-- =============================================
-- Proyecto: Sigein 5.0
-- Copyright (c) - Acrux - 2015
-- Author: Julio Díaz
-- CREATE date: 23/11/2015
-- Description: Obtiene los roles del sistema
-- =============================================

CREATE PROCEDURE [ADM].[SPE_OBTIENE_ROL]
	@PIN_ID_ROL AS INT = NULL
AS   
BEGIN
	DECLARE @ID_ROL INT
		, @CL_ROL NVARCHAR(30)
		, @NB_ROL NVARCHAR(100)
		, @FG_ACTIVO BIT
		, @XML_AUTORIZACION XML

	SELECT @ID_ROL = CR.ID_ROL
		, @CL_ROL = CR.CL_ROL
		, @NB_ROL = CR.NB_ROL
		, @FG_ACTIVO = CR.FG_ACTIVO
	FROM ADM.C_ROL CR
	WHERE CR.ID_ROL = @PIN_ID_ROL

	SET @XML_AUTORIZACION = (
		SELECT SF.ID_FUNCION AS "@ID_FUNCION"
			, SF.CL_FUNCION AS "@CL_FUNCION"
			, SF.CL_TIPO_FUNCION AS "@CL_TIPO_FUNCION"
			, SF.ID_FUNCION_PADRE AS "@ID_FUNCION_PADRE"
			, SF.NB_FUNCION AS "@NB_FUNCION"
			, CASE WHEN CRF.ID_FUNCION IS NOT NULL THEN 1 ELSE 0 END AS "@FG_SELECCIONADO"
		FROM ADM.S_FUNCION SF
			LEFT JOIN ADM.C_ROL_FUNCION CRF
				ON SF.ID_FUNCION = CRF.ID_FUNCION
				AND CRF.ID_ROL = @PIN_ID_ROL
		FOR XML PATH ('FUNCION'), ROOT ('FUNCIONES')
	)

	SELECT ISNULL(@ID_ROL, -1) AS ID_ROL
		, @CL_ROL AS CL_ROL
		, @NB_ROL AS NB_ROL
		, ISNULL(@FG_ACTIVO, 0) AS FG_ACTIVO
		, @XML_AUTORIZACION AS XML_AUTORIZACION
END