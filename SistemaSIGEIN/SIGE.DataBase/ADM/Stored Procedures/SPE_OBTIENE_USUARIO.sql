-- =============================================
-- Proyecto: Sigein 5.0
-- Copyright (c) - Acrux - 2015
-- Author: Julio Díaz
-- CREATE date: 25/11/2015
-- Description: Obtiene los usuarios del sistema
-- =============================================

CREATE PROCEDURE [ADM].[SPE_OBTIENE_USUARIO]
	@PIN_CL_USUARIO AS NVARCHAR(50) = NULL
AS   
BEGIN
	DECLARE @CL_USUARIO NVARCHAR(50)
		, @NB_USUARIO NVARCHAR(300)
		, @NB_CORREO_ELECTRONICO NVARCHAR(500)
		, @FG_ACTIVO BIT
		, @XML_ROLES XML
		, @XML_EMPLEADOS XML
		, @XML_CATALOGOS XML

	SELECT @CL_USUARIO = CU.CL_USUARIO
		, @NB_USUARIO = CU.NB_USUARIO
		, @NB_CORREO_ELECTRONICO = CU.NB_CORREO_ELECTRONICO
		, @FG_ACTIVO = CU.FG_ACTIVO
	FROM ADM.C_USUARIO CU
	WHERE CU.CL_USUARIO = @PIN_CL_USUARIO

	SET @XML_ROLES = (
		SELECT CR.ID_ROL AS "@ID_ROL"
			, CR.CL_ROL AS "@CL_ROL"
			, CR.NB_ROL AS "@NB_ROL"
			, CASE WHEN CU.CL_USUARIO IS NOT NULL THEN 1 ELSE 0 END AS "@FG_SELECCIONADO"
		FROM ADM.C_ROL CR
			LEFT JOIN ADM.C_USUARIO CU
				ON CR.ID_ROL = CU.ID_ROL
				AND CU.CL_USUARIO = @PIN_CL_USUARIO
		FOR XML PATH ('ROL'), ROOT ('ROLES')
	)

	SET @XML_EMPLEADOS = (
		SELECT ME.ID_EMPLEADO AS "@ID_EMPLEADO"
			, ME.NB_EMPLEADO + ' ' + ME.NB_APELLIDO_PATERNO + ' ' + ME.NB_APELLIDO_MATERNO AS "@NB_EMPLEADO"
			, MP.NB_PUESTO AS "@NB_PUESTO"
			, MD.NB_DEPARTAMENTO AS "@NB_DEPARTAMENTO"
			, CASE WHEN CU.CL_USUARIO IS NOT NULL THEN 1 ELSE 0 END AS "@FG_SELECCIONADO"
		FROM ADM.M_EMPLEADO ME
			INNER JOIN ADM.M_PUESTO MP
				ON ME.ID_PUESTO = MP.ID_PUESTO
			INNER JOIN ADM.M_DEPARTAMENTO MD
				ON MP.ID_DEPARTAMENTO = MD.ID_DEPARTAMENTO
			INNER JOIN ADM.C_USUARIO CU
				ON CU.ID_EMPLEADO = ME.ID_EMPLEADO
				AND CU.CL_USUARIO = @PIN_CL_USUARIO
		FOR XML PATH ('EMPLEADO'), ROOT ('EMPLEADOS')
	)

	SET @XML_CATALOGOS = (
		SELECT @XML_ROLES
			, @XML_EMPLEADOS
		FOR XML PATH ('CATALOGOS')
	)

	SELECT @CL_USUARIO AS CL_USUARIO
		, @NB_USUARIO AS NB_USUARIO
		, @NB_CORREO_ELECTRONICO AS NB_CORREO_ELECTRONICO
		, ISNULL(@FG_ACTIVO, 0) AS FG_ACTIVO
		, @XML_CATALOGOS AS XML_CATALOGOS
END