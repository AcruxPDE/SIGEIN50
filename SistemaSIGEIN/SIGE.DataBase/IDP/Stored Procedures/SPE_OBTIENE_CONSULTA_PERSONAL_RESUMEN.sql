CREATE PROCEDURE [IDP].[SPE_OBTIENE_CONSULTA_PERSONAL_RESUMEN]

	@PIN_ID_BATERIA AS INT

AS
BEGIN

	DECLARE @V_ID_CUESTIONARIO_BAREMOS AS INT

	SELECT @V_ID_CUESTIONARIO_BAREMOS = ID_CUESTIONARIO_BAREMOS
	FROM IDP.K_BATERIA_PRUEBA
	WHERE ID_BATERIA = @PIN_ID_BATERIA

	;WITH T_PROMEDIO AS (

		SELECT 
			CL_CLASIFICACION, 
			ID_COMPETENCIA,
			NB_COMPETENCIA, 
			DS_COMPETENCIA,
			CL_COLOR,
			CAST(AVG(NO_VALOR) AS DECIMAL(13,1)) AS NO_BAREMO_PROMEDIO
		FROM IDP.F_OBTIENE_DATOS_CONSULTA_PERSONAL(@V_ID_CUESTIONARIO_BAREMOS)
		GROUP BY CL_CLASIFICACION, ID_COMPETENCIA, NB_COMPETENCIA, DS_COMPETENCIA, CL_COLOR)
	, T_PORCENTAJE AS (
	
		SELECT
				CL_CLASIFICACION,
				ID_COMPETENCIA,
				NB_COMPETENCIA,
				DS_COMPETENCIA,
				CL_COLOR,
				NO_BAREMO_PROMEDIO,
				((NO_BAREMO_PROMEDIO / 3) * 100) as NO_BAREMO_PORCENTAJE,
			CASE 
				WHEN NO_BAREMO_PROMEDIO >= 2.8 THEN 5
				WHEN NO_BAREMO_PROMEDIO BETWEEN 2.5 AND 2.7 THEN 4
				WHEN NO_BAREMO_PROMEDIO BETWEEN 2.1 AND 2.4 THEN 3
				WHEN NO_BAREMO_PROMEDIO BETWEEN 1.7 AND 2.0 THEN 2
				WHEN NO_BAREMO_PROMEDIO BETWEEN 1.3 AND 1.6 THEN 1
				ELSE 0 END AS NO_BAREMO_FACTOR
		FROM T_PROMEDIO
	)

	SELECT TP.CL_CLASIFICACION, TP.ID_COMPETENCIA, TP.NB_COMPETENCIA, TP.DS_COMPETENCIA, TP.CL_COLOR, TP.NO_BAREMO_PROMEDIO, TP.NO_BAREMO_PORCENTAJE, TP.NO_BAREMO_FACTOR, CCN.DS_NIVEL_COMPETENCIA_PERSONA
	FROM T_PORCENTAJE TP
		LEFT OUTER JOIN ADM.C_COMPETENCIA_NIVEL CCN ON TP.ID_COMPETENCIA = CCN.ID_COMPETENCIA AND TP.NO_BAREMO_FACTOR = CAST(CCN.NO_VALOR_NIVEL AS INT)

END